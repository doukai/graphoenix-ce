plugins {
    id 'java-library'
    id "com.github.node-gradle.node" version "$nodeGradlePluginVersion"
    id 'maven-publish'
}

group 'org.graphoenix'

dependencies {
    implementation platform(project(':graphoenix-platform'))
    implementation platform("org.graphoenix:nozdormu-platform:$nozdormuVersion")

    implementation project(':graphoenix-spi')
    implementation 'org.graphoenix:nozdormu-inject'
    implementation 'org.graphoenix:nozdormu-config'
    implementation "io.javalin:javalin"
    implementation 'org.eclipse.jetty:jetty-proxy'
    implementation 'com.google.auto.service:auto-service'

    annotationProcessor project(':graphoenix-spi')
    annotationProcessor 'org.graphoenix:nozdormu-inject'
    annotationProcessor 'org.graphoenix:nozdormu-config'
    annotationProcessor 'com.google.auto.service:auto-service'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

node {
    nodeProjectDir = file("${project.projectDir}/webapp")
}

def pnpmInstall = tasks.named("pnpmInstall")

def buildTask = tasks.register("buildWebapp", NpxTask) {
    command = "react-scripts"
    args = ["build"]
//    dependsOn(pnpmInstall)
    inputs.dir(fileTree("webapp").exclude("**/*.test.js").exclude("**/*.spec.js").exclude("**/__tests__/**/*.js"))
    inputs.dir("webapp/node_modules")
    inputs.dir("webapp/public")
    outputs.dir("${buildDir}/webapp")
    environment = ["BUILD_PATH": "${buildDir}/webapp/webroot"]
}

sourceSets {
    java {
        main {
            resources {
                // This makes the processResources task automatically depend on the buildWebapp one
                srcDir(buildTask)
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}