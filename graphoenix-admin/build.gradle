plugins {
    id 'java-library'
    id "com.github.node-gradle.node" version "7.0.2"
    id 'maven-publish'
}

group 'org.graphoenix'
version '0.1.1'

repositories {
    mavenCentral()
    jcenter()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation project(':graphoenix-spi')
    implementation 'org.graphoenix:nozdormu-inject:0.1.0'
    implementation 'org.graphoenix:nozdormu-config:0.1.0'
    implementation "io.javalin:javalin:6.1.3"
    implementation 'org.eclipse.jetty:jetty-proxy:11.0.20'
    implementation 'com.google.auto.service:auto-service:1.0.1'

    annotationProcessor project(':graphoenix-spi')
    annotationProcessor 'org.graphoenix:nozdormu-inject:0.1.0'
    annotationProcessor 'org.graphoenix:nozdormu-config:0.1.0'
    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

node {
    nodeProjectDir = file("${project.projectDir}/webapp")
}

def pnpmInstall = tasks.named("pnpmInstall")

def buildTask = tasks.register("buildWebapp", NpxTask) {
    command = "react-scripts"
    args = ["build"]
//    dependsOn(pnpmInstall)
    inputs.dir(fileTree("webapp").exclude("**/*.test.js").exclude("**/*.spec.js").exclude("**/__tests__/**/*.js"))
    inputs.dir("webapp/node_modules")
    inputs.dir("webapp/public")
    outputs.dir("${buildDir}/webapp")
    environment = ["BUILD_PATH": "${buildDir}/webapp/webroot"]
}

sourceSets {
    java {
        main {
            resources {
                // This makes the processResources task automatically depend on the buildWebapp one
                srcDir(buildTask)
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.graphoenix'
            artifactId = 'graphoenix-admin'

            from components.java

            pom {
                name = 'graphoenix-admin'
                description = 'graphoenix-admin'
                url = 'https://github.com/doukai/graphoenix-ce'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'doukai'
                        name = 'Dou Kai'
                    }
                }
                scm {
                    url = 'https://github.com/doukai/graphoenix-ce'
                    connection = 'scm:git://github.com:doukai/graphoenix-ce.git'
                    developerConnection = 'scm:git://github.com:doukai/graphoenix-ce.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

tasks.build.doFirst {
    mkdir "$buildDir/jreleaser"
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}