buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.graphoenix:graphoenix-gradle-plugin:$graphoenixVersion"
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version "$protobufPluginVersion"
    id 'maven-publish'
}

apply plugin: 'io.graphoenix'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$protocGenGrpcJavaVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
        }
    }
}

group 'org.graphoenix'

dependencies {
    implementation platform(project(':graphoenix-platform'))
    implementation platform("org.graphoenix:nozdormu-platform:$nozdormuVersion")

    implementation project(':graphoenix-core')
    implementation 'org.graphoenix:nozdormu-inject'
    implementation 'org.graphoenix:nozdormu-interceptor'
    implementation 'org.graphoenix:nozdormu-config'
    implementation 'com.google.guava:guava'

    runtimeOnly 'io.grpc:grpc-netty-shaded'
    implementation 'io.grpc:grpc-protobuf'
    implementation 'io.grpc:grpc-stub'

    compileOnly 'org.apache.tomcat:annotations-api' // necessary for Java 9+

    annotationProcessor project(':graphoenix-annotation-processor')
    annotationProcessor 'org.graphoenix:nozdormu-inject'
    annotationProcessor 'org.graphoenix:nozdormu-interceptor'
    annotationProcessor 'org.graphoenix:nozdormu-config'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}