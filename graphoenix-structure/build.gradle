buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.1'
    id 'maven-publish'
    id "org.graphoenix" version "0.1.1"
}

protobuf {
    protoc {
        // artifact = "com.google.protobuf:protoc:3.21.7"
        // for apple m1, please add protoc_platform=osx-x86_64 in $HOME/.gradle/gradle.properties
        if (project.hasProperty('protoc_platform')) {
            artifact = "com.google.protobuf:protoc:3.21.7:${protoc_platform}"
        } else {
            artifact = "com.google.protobuf:protoc:3.21.7"
        }
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.52.1'
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.3"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactor {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/reactor'
        }
    }
}

group 'org.graphoenix'
version '0.1.2'

repositories {
    mavenCentral()
    jcenter()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

sourcesJar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

dependencies {
    implementation project(':graphoenix-core')
    implementation 'org.graphoenix:nozdormu-inject:0.1.0'
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.52.1'
    implementation 'io.grpc:grpc-protobuf:1.52.1'
    implementation 'io.grpc:grpc-stub:1.52.1'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    annotationProcessor project(':graphoenix-annotation-processor')
    annotationProcessor 'org.graphoenix:nozdormu-inject:0.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.graphoenix'
            artifactId = 'graphoenix-structure'

            from components.java

            pom {
                name = 'graphoenix-structure'
                description = 'graphoenix-structure'
                url = 'https://github.com/doukai/graphoenix-ce'
                inceptionYear = '2023'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'doukai'
                        name = 'Dou Kai'
                    }
                }
                scm {
                    url = 'https://github.com/doukai/graphoenix-ce'
                    connection = 'scm:git://github.com:doukai/graphoenix-ce.git'
                    developerConnection = 'scm:git://github.com:doukai/graphoenix-ce.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

tasks.build.doFirst {
    mkdir "$buildDir/jreleaser"
}

jreleaser {
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}