buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.graphoenix:graphoenix-gradle-plugin:$graphoenixVersion"
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version "$protobufPluginVersion"
    id 'maven-publish'
}

apply plugin: 'io.graphoenix'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$protocGenGrpcJavaVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
        }
    }
}

group 'org.graphoenix'

dependencies {
    implementation platform(project(':graphoenix-platform'))

    api project(':graphoenix-spi')
    api project(':graphoenix-json')
    api 'com.google.auto.service:auto-service'
    api 'com.github.ben-manes.caffeine:caffeine'

    implementation 'org.graphoenix:nozdormu-inject'
    implementation 'org.graphoenix:nozdormu-interceptor'
    implementation 'org.graphoenix:nozdormu-config'
    implementation 'com.google.guava:guava'
    implementation 'com.aventrix.jnanoid:jnanoid'
    implementation 'org.tinylog:tinylog-impl'
    implementation 'org.tinylog:slf4j-tinylog'
    implementation 'org.tinylog:jcl-tinylog'

    runtimeOnly 'io.grpc:grpc-netty-shaded'
    implementation 'io.grpc:grpc-protobuf'
    implementation 'io.grpc:grpc-stub'

    compileOnly 'org.apache.tomcat:annotations-api' // necessary for Java 9+

    annotationProcessor project(':graphoenix-spi')
    annotationProcessor project(':graphoenix-json')
    annotationProcessor 'org.graphoenix:nozdormu-inject'
    annotationProcessor 'org.graphoenix:nozdormu-interceptor'
    annotationProcessor 'org.graphoenix:nozdormu-config'
    annotationProcessor 'com.google.auto.service:auto-service'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}