buildscript {
    repositories {
        mavenLocal()
        jcenter()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'org.graphoenix:graphoenix-gradle-plugin:0.1.0-SNAPSHOT'
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.1'
    id 'maven-publish'
}

apply plugin: 'io.graphoenix'

protobuf {
    protoc {
        // artifact = "com.google.protobuf:protoc:3.21.7"
        // for apple m1, please add protoc_platform=osx-x86_64 in $HOME/.gradle/gradle.properties
        if (project.hasProperty('protoc_platform')) {
            artifact = "com.google.protobuf:protoc:3.21.7:${protoc_platform}"
        } else {
            artifact = "com.google.protobuf:protoc:3.21.7"
        }
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.52.1'
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.3"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactor {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/reactor'
        }
    }
}

group 'org.graphoenix'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

dependencies {
    api project(':graphoenix-spi')
    api project(':graphoenix-json')
    api 'com.google.auto.service:auto-service:1.0.1'

    implementation 'org.graphoenix:nozdormu-inject:0.1.0-SNAPSHOT'
    implementation 'org.graphoenix:nozdormu-interceptor:0.1.0-SNAPSHOT'
    implementation 'org.graphoenix:nozdormu-config:0.1.0-SNAPSHOT'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.aventrix.jnanoid:jnanoid:2.0.0'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.1'
    implementation 'org.tinylog:tinylog-impl:2.4.1'
    implementation 'org.tinylog:slf4j-tinylog:2.4.1'
    implementation 'org.tinylog:jcl-tinylog:2.4.1'

    runtimeOnly 'io.grpc:grpc-netty-shaded:1.52.1'
    implementation 'io.grpc:grpc-protobuf:1.52.1'
    implementation 'io.grpc:grpc-stub:1.52.1'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    annotationProcessor project(':graphoenix-spi')
    annotationProcessor project(':graphoenix-json')
    annotationProcessor 'org.graphoenix:nozdormu-inject:0.1.0-SNAPSHOT'
    annotationProcessor 'org.graphoenix:nozdormu-interceptor:0.1.0-SNAPSHOT'
    annotationProcessor 'org.graphoenix:nozdormu-config:0.1.0-SNAPSHOT'
    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}